plugins {
    id 'java'
    id 'application'
}

group = 'es.jklabs'
version = project.findProperty('appVersion') ?: '1.0.5'
def appVersion = project.version.toString()

application {
    mainClass = "es.jklabs.BeyondSQLExecutor"
}

distributions {
    main {
        contents {
            from("README.md") {
                into ""
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    implementation 'com.mysql:mysql-connector-j:9.6.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.7'
    implementation 'org.postgresql:postgresql:42.7.9'
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'software.amazon.awssdk:rds:2.41.22'
    implementation 'software.amazon.awssdk:sts:2.41.22'
    implementation 'com.fifesoft:rsyntaxtextarea:3.6.1'
    implementation 'net.java.dev.jna:jna:5.18.1'
    implementation 'net.java.dev.jna:jna-platform:5.18.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.2'
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching("build.properties") {
        expand(version: appVersion)
    }
}

def installerType = project.findProperty('installerType') ?: 'deb'
def installerIcon = project.findProperty('installerIcon') ?: 'src/main/resources/img/icons/database-installer.png'
def linuxResourceDir = file('src/main/resources/packaging/linux')
def appDescription = 'Lanzador de sentencias SQL de manera masiva contra varias bases de datos: MySQL, MariaDB o PostgreSQL'
def appLicenseFile = file('LICENSE')
def appAboutUrl = 'https://github.com/JCPrieto/BeyondSQLExecutor'

def jpackageInputDir = layout.buildDirectory.dir('jpackage/input')
def jpackageOutputDir = layout.buildDirectory.dir('jpackage')

tasks.register('prepareJpackageInput', Copy) {
    from configurations.runtimeClasspath
    from tasks.named('jar')
    from('src/main/resources/packaging/linux/beyondsqlexecutor-BeyondSQLExecutor.metainfo.xml')
    into jpackageInputDir
}

tasks.register('jpackage', Exec) {
    dependsOn tasks.named('prepareJpackageInput')
    def javaHome = System.getProperty('java.home')
    def jpackageBin = new File(javaHome, 'bin/jpackage').absolutePath
    def args = [
            jpackageBin,
            '--type', installerType,
            '--name', 'BeyondSQLExecutor',
            '--app-version', appVersion,
            '--input', jpackageInputDir.get().asFile.absolutePath,
            '--main-jar', "BeyondSQLExecutor-${appVersion}.jar",
            '--main-class', 'es.jklabs.BeyondSQLExecutor',
            '--description', appDescription,
            '--license-file', appLicenseFile.absolutePath,
            '--about-url', appAboutUrl,
            '--icon', installerIcon,
            '--dest', jpackageOutputDir.get().asFile.absolutePath
    ]

    if (installerType in ['deb', 'rpm']) {
        args += [
                '--linux-shortcut',
                '--linux-menu-group', 'Development',
                '--linux-app-category', 'Development',
                '--resource-dir', linuxResourceDir.absolutePath,
                '--java-options', '-Dsun.awt.X11.awtAppClassName=es-jklabs-BeyondSQLExecutor'
        ]
    }

    commandLine(args)
}
